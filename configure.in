dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(sodipodi, 0.25)
AM_CONFIG_HEADER(config.h)

dnl Pick up the Gnome macros.
AM_ACLOCAL_INCLUDE(macros)

GNOME_INIT
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

GNOME_COMPILE_WARNINGS
GNOME_X_CHECKS
GNOME_XML_CHECK

dnl GNOME_PRINT_CHECK(0.21)
GNOME_PRINT_LIBS=`gnome-config --libs print`
AC_SUBST(GNOME_PRINT_LIBS)

AC_CHECK_HEADER(libgnomeprint/gnome-print-frgba.h,AC_DEFINE(ENABLE_FRGBA))
AC_CHECK_HEADER(libgnomeprint/gnome-print-rbuf.h,AC_DEFINE(ENABLE_RBUF))
AC_CHECK_HEADER(libgnomeprint/gnome-font-face.h,AC_DEFINE(NEW_FONT_MODEL))

dnl Add the languages which your application supports here.
ALL_LINGUAS="ca da de es et fr ga gl hu it ja no ru sl sv tr uk pl pt_BR"
AM_GNU_GETTEXT

dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl ******************************
dnl Gnome App Lib checking
dnl ******************************
AC_MSG_CHECKING(for Gnome App libraries (GAL) >= 0.4)
if gnome-config --libs gal > /dev/null 2>&1; then 
    vers=`gnome-config --modversion gal | sed -e "s/gal-//" -e 's/cvs$//' -e 's/pre$//' | \
        awk 'BEGIN { FS = "."; } { print $1 * 1000000 + $2 * 10000 + $3 * 100 + $4;}'`
    if test "$vers" -ge 40000; then
        AC_MSG_RESULT(found)
    else
        AC_MSG_ERROR(You need at least GNOME Application libs 0.4 for this version of Sodipodi)
    fi
else
    AC_MSG_ERROR(Did not find GnomeAppLib (GAL) installed)
fi

dnl ******************************
dnl Check for Bonobo
dnl ******************************
try_bonobo=true
have_bonobo=false
AC_ARG_WITH(bonobo,
	[--{with,without}-bonobo Compile with Bonobo support or without it],
	if test x$withval = xno; then
		try_bonobo=false
	fi
)

if $try_bonobo; then
	AC_MSG_CHECKING(for Bonobo >= 0.37)
	if $GNOME_CONFIG --libs bonobox_print > /dev/null 2>&1; then
		vers=`$GNOME_CONFIG --modversion bonobo | sed -e "s/bonobo-//" | \
			awk 'BEGIN { FS = "."; } { print $1 * 1000 + $2;}'`
		if test "$vers" -ge 37; then
			bonobo_ok=true
		else
			bonobo_ok=false
		fi
	else
		bonobo_ok=false
	fi

	if $bonobo_ok; then
		AC_MSG_RESULT(found)
		AC_DEFINE(ENABLE_BONOBO)
		have_bonobo=true
		SODIPODI_BONOBO_LIBS=`$GNOME_CONFIG --libs bonobox_print`
		SODIPODI_BONOBO_CFLAGS=`$GNOME_CONFIG --cflags bonobox_print`

		dnl Check if Bonobo is OAFized

		AC_MSG_CHECKING(if Bonobo uses OAF)
		if ( gnome-config --libs bonobo | grep oaf ) > /dev/null 2>&1 ; then
			using_oaf="yes"
			AC_DEFINE(USING_OAF)
		else
			using_oaf="no"
		fi

		AC_MSG_RESULT("$using_oaf")

		AM_CONDITIONAL(USING_OAF, test x"using_oaf" = "xyes")
	else    
		AC_MSG_RESULT(not found)
		SODIPODI_BONOBO_LIBS=
		SODIPODI_BONOBO_CFLAGS=
	fi
fi

AM_CONDITIONAL(BONOBO, $have_bonobo)
AC_SUBST(SODIPODI_BONOBO_CFLAGS)
AC_SUBST(SODIPODI_BONOBO_LIBS)

dnl ******************************
dnl GnomePrint checking
dnl ******************************
AC_MSG_CHECKING(for GnomePrint libraries >= 0.21)
if gnome-config --libs print > /dev/null 2>&1; then
vers=`gnome-config --modversion print | sed -e "s/gnome-print-//" -e 's/cvs$//' | \
        awk 'BEGIN { FS = "."; } { print $1 * 1000 + $2;}'`
    if test "$vers" -ge 21; then
        AC_MSG_RESULT(found)
    else
         AC_MSG_ERROR(You need at least GNOME print 0.21 for this version of sodipodi)
    fi
else
    AC_MSG_ERROR(Did not find GnomePrint installed)
fi

use_new_render=false
AC_ARG_WITH(new-render,
	[--{with,without}-new-render Compile with experimental rendering engine],
	if test x$withval = xyes; then
		use_new_render=true
		AC_DEFINE(NEW_RENDER)
	fi
)
AM_CONDITIONAL(WITH_NEW_RENDER, $use_new_render)
AC_SUBST(WITH_NEW_RENDER)

use_arena=false
AC_ARG_WITH(arena,
	[--{with,without}-arena Compile with experimental canvas engine],
	if test x$withval = xyes; then
		use_arena=true
		AC_DEFINE(ARENA)
	fi
)
AM_CONDITIONAL(WITH_ARENA, $use_arena)
AC_SUBST(WITH_ARENA)

XML_KEY=xml
AC_ARG_WITH(gnome-xml2,
	[--{with,without}-gnome-xml2 Use gnome-config {--cflags|--libs} xml2],
	if test x$withval = xyes; then
		XML_KEY=xml2
	fi
)

SODIPODI_CFLAGS=`$GNOME_CONFIG --cflags gal gdk_pixbuf gdk_pixbuf_xlib libart libglade print $XML_KEY`
SODIPODI_LIBS=`$GNOME_CONFIG --libs gal gdk_pixbuf gdk_pixbuf_xlib libart libglade print $XML_KEY`
SODIPODI_LIBS="$SODIPODI_LIBS -lpng -lz"

dnl ******************************
dnl system libwmf support
dnl ******************************

dnl Check for libwmf-0.2.1 or higher (experimental)

_cppflags=$CPPFLAGS
_ldflags=$LDFLAGS

have_libwmf=no

AC_ARG_WITH(libwmf,
	[--{with,without}-libwmf=DIR use libwmf(2) in DIR (experimental)],[
	if [ test "x$withval" != "xno" ]; then
		if [ test "x$withval" != "xyes" ]; then
			LIBWMF_DIR=$withval
		fi
		search_for_libwmf=yes
	else
		search_for_libwmf=no
	fi
],[	search_for_libwmf=no
])

if [ test $search_for_libwmf != no ]; then
	if [ test -n "$LIBWMF_DIR" ]; then
		AC_PATH_PROG(LIBWMF_CONFIG,libwmf-config, ,[$LIBWMF_DIR/bin:$PATH])
	else
		AC_PATH_PROG(LIBWMF_CONFIG,libwmf-config)
	fi

	if [ test -n "$LIBWMF_CONFIG" ]; then
		libwmf_cflags=`$LIBWMF_CONFIG --cflags`
		libwmf_libs=`$LIBWMF_CONFIG --libs`
	else
		AC_MSG_ERROR(* * * unable to find libwmf-config; unable to continue * * *)
	fi

	CPPFLAGS="$CPPFLAGS $libwmf_cflags"
	LDFLAGS="$LDFLAGS $libwmf_libs"

	AC_CHECK_HEADER(libwmf/svg.h,[
		AC_CHECK_LIB(wmf,wmf_stream_create,have_libwmf=yes,[
			AC_MSG_ERROR(* * * libwmf version 0.2.1 or later * * *)
		])
	])

	if [ test $have_libwmf != no]; then
		SODIPODI_LIBS="$SODIPODI_LIBS $libwmf_libs"
		SODIPODI_CFLAGS="$SODIPODI_CFLAGS $libwmf_cflags -DHAVE_LIBWMF=1"
	fi
fi

CPPFLAGS=$_cppflags
LDFLAGS=$_ldflags

AC_SUBST(SODIPODI_CFLAGS)
AC_SUBST(SODIPODI_LIBS)

AC_OUTPUT([
Makefile
macros/Makefile
src/Makefile
src/xml/Makefile
src/svg/Makefile
src/widgets/Makefile
src/helper/Makefile
src/dialogs/Makefile
src/display/Makefile
src/display/nr/Makefile
src/bonobo/Makefile
glade/Makefile
intl/Makefile
po/Makefile.in
sodipodi.spec
sodipodi.1
])
