dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(sodipodi, 0.18)
AM_CONFIG_HEADER(config.h)

dnl Pick up the Gnome macros.
AM_ACLOCAL_INCLUDE(macros)

GNOME_INIT
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

GNOME_COMPILE_WARNINGS
GNOME_X_CHECKS

GNOME_PRINT_CHECK
AC_CHECK_HEADER(libgnomeprint/gnome-print-frgba.h,AC_DEFINE(ENABLE_FRGBA))
AC_CHECK_HEADER(libgnomeprint/gnome-print-rbuf.h,AC_DEFINE(ENABLE_RBUF))

dnl Add the languages which your application supports here.
ALL_LINGUAS="da et no ru uk"
AM_GNU_GETTEXT

dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl Subst PACKAGE_PIXMAPS_DIR.
dnl PACKAGE_PIXMAPS_DIR="`gnome-config --datadir`/pixmaps/${PACKAGE}"
PACKAGE_PIXMAPS_DIR="`gnome-config --datadir`/${PACKAGE}"
AC_SUBST(PACKAGE_PIXMAPS_DIR)
PACKAGE_GLADE_DIR="`gnome-config --datadir`/${PACKAGE}"
AC_SUBST(PACKAGE_GLADE_DIR)

dnl ******************************
dnl Check for Bonobo
dnl ******************************
try_bonobo=true
have_bonobo=false
AC_ARG_WITH(bonobo,
	[--{with,without}-bonobo Compile with Bonobo support or without it],
	if test x$withval = xno; then
		try_bonobo=false
	fi
)

if $try_bonobo; then
	AC_MSG_CHECKING(for Bonobo > 0.9)
	if $GNOME_CONFIG --libs bonobo > /dev/null 2>&1; then
		vers=`$GNOME_CONFIG --modversion bonobo`
		case $vers
		in
			bonobo-0.[[012345678]]) bonobo_ok=false ;;
			*) bonobo_ok=true ;;
		esac
	else
		bonobo_ok=false
	fi

	if $bonobo_ok; then
		AC_MSG_RESULT(found)
		AC_DEFINE(ENABLE_BONOBO)
		have_bonobo=true
		EXTRA_BONOBO_GNOME_LIBS=`$GNOME_CONFIG --libs bonobo`
		EXTRA_BONOBO_GNOME_CFLAGS=`$GNOME_CONFIG --cflags bonobo`
	else    
		AC_MSG_RESULT(not found)
		EXTRA_BONOBO_GNOME_LIBS=
		EXTRA_BONOBO_GNOME_CFLAGS=
	fi
fi

AM_CONDITIONAL(BONOBO, $have_bonobo)
AC_SUBST(EXTRA_BONOBO_GNOME_CFLAGS)
AC_SUBST(EXTRA_BONOBO_GNOME_LIBS)

AC_OUTPUT([
Makefile
macros/Makefile
src/Makefile
src/xml/Makefile
src/svg/Makefile
src/helper/Makefile
src/dialogs/Makefile
src/display/Makefile
src/bonobo/Makefile
intl/Makefile
po/Makefile.in
sodipodi.spec
])
